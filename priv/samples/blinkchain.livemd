# Blinkchain Example

## Setup the Display

```elixir
defmodule Rainbow.Worker do
  use GenServer

  # Arrangement looks like this:
  # Y  X: 0  1  2  3  4  5  6  7
  #    |-------------------------|
  # 0  |  0  1  2  3  4  5  6  7 |
  # 1  |  8  9 10 11 12 13 14 15 | <- Pimoroni Unicorn pHat on Channel 0 (pin 18)
  # 2  | 16 17 18 19 20 21 22 23 |
  # 3  | 24 25 26 27 28 29 30 31 |
  #    |-------------------------|

  alias Blinkchain.Color
  alias Blinkchain.Point

  defmodule State do
    defstruct [:timer, :colors]
  end

  def start_link(opts \\ []) do
    GenServer.start_link(__MODULE__, opts, name: __MODULE__)
  end

  @colors [
    Color.parse("#9400D3"),
    Color.parse("#4B0082"),
    Color.parse("#0000FF"),
    Color.parse("#00FF00"),
    Color.parse("#FFFF00"),
    Color.parse("#FF7F00"),
    Color.parse("#FF0000")
  ]

  def init(_opts) do
    # Send ourselves a message to draw each frame every 33 ms,
    # which will end up being approximately 15 fps.
    {:ok, ref} = :timer.send_interval(33, :draw_frame)

    state = %State{
      timer: ref,
      colors: @colors
    }

    {:ok, state}
  end

  def handle_info(:draw_frame, state) do
    [c1, c2, c3, c4] = Enum.slice(state.colors, 0..3)
    tail = Enum.slice(state.colors, 1..-1)

    # Shift all pixels to the right
    Blinkchain.copy(%Point{x: 0, y: 0}, %Point{x: 1, y: 0}, 7, 4)

    # Populate the five leftmost pixels with new colors
    Blinkchain.set_pixel(%Point{x: 0, y: 0}, c1)
    Blinkchain.set_pixel(%Point{x: 0, y: 1}, c2)
    Blinkchain.set_pixel(%Point{x: 0, y: 2}, c3)
    Blinkchain.set_pixel(%Point{x: 0, y: 3}, c4)

    Blinkchain.render()
    {:noreply, %State{state | colors: tail ++ [c1]}}
  end
end
```

```elixir
Rainbow.Worker.start_link()
```
